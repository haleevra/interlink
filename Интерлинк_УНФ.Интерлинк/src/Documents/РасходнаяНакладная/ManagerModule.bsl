
&Вместо("ДанныеДокументовРегУчет")
Функция Интерлинк_ДанныеДокументовРегУчет(МассивОбъектов, ИспользоватьФаксимиле, ПечатнаяФормаТолькоВРублях, Ошибки)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("НациональнаяВалюта", Константы.НациональнаяВалюта.Получить());
	Запрос.УстановитьПараметр("ПечатнаяФормаТолькоВРублях", ПечатнаяФормаТолькоВРублях);
	Запрос.УстановитьПараметр("ШтукаНаименование", Справочники.КлассификаторЕдиницИзмерения.шт.Наименование);
	Запрос.УстановитьПараметр("ШтукаКод", Справочники.КлассификаторЕдиницИзмерения.шт.Код);
	Запрос.УстановитьПараметр("ИспользоватьФаксимиле", ИспользоватьФаксимиле);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КонтурEDI_СоответствияТоваров.НаименованиеПартнера КАК НаименованиеПартнера,
	|	КонтурEDI_СоответствияТоваров.КодТовараПартнера КАК КодТовараПартнера,
	|	КонтурEDI_СоответствияТоваров.Номенклатура КАК Номенклатура,
	|	КонтурEDI_ДополнительныеРеквизиты.Объект КАК Партнер
	|ПОМЕСТИТЬ ВТ_ДанныеEDI
	|ИЗ
	|	Документ.РасходнаяНакладная КАК РасходнаяНакладная
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КонтурEDI_ДополнительныеРеквизиты КАК КонтурEDI_ДополнительныеРеквизиты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КонтурEDI_СоответствияТоваров КАК КонтурEDI_СоответствияТоваров
	|			ПО (КонтурEDI_СоответствияТоваров.Партнер = КонтурEDI_ДополнительныеРеквизиты.Значение)
	|		ПО РасходнаяНакладная.Контрагент = КонтурEDI_ДополнительныеРеквизиты.Объект
	|			И (КонтурEDI_ДополнительныеРеквизиты.Свойство = ""ПартнерКонтрагента"")
	|ГДЕ
	|	РасходнаяНакладная.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|Выбрать
	|	РасходнаяНакладная.Ссылка КАК Ссылка
	|	,РасходнаяНакладная.Дата КАК ДатаДокумента
	|	,Выразить(РасходнаяНакладная.Номер КАК Строка(12)) КАК Номер
	|	,Неопределено КАК НомерИсправления
	|	,Неопределено КАК ДатаИсправления
	|	,Неопределено КАК ФаксимилеПустая
	|	,Значение(Перечисление.ВидыОперацийСчетФактура.Продажа) КАК ВидОперации
	|	,Выбор когда РасходнаяНакладная.Организация.ГоловнаяОрганизация <> Значение(Справочник.Организации.ПустаяСсылка)
	|		Тогда РасходнаяНакладная.Организация.ГоловнаяОрганизация
	|		Иначе РасходнаяНакладная.Организация Конец КАК Организация
	|	,РасходнаяНакладная.Организация.Префикс КАК Префикс
	|	,РасходнаяНакладная.Организация.ФайлЛоготип КАК Логотип
	|	,РасходнаяНакладная.Организация.ФайлФаксимильнаяПечать КАК ФаксимилеПечати
	|	,Выбор когда РасходнаяНакладная.Организация.ГоловнаяОрганизация <> Значение(Справочник.Организации.ПустаяСсылка)
	|			Тогда РасходнаяНакладная.Организация.ЦифровойИндексОбособленногоПодразделения
	|		когда РасходнаяНакладная.Подразделение.ГоловнаяОрганизация <> Значение(Справочник.Организации.ПустаяСсылка)
	|				И РасходнаяНакладная.Подразделение.ГоловнаяОрганизация = РасходнаяНакладная.Организация
	|			Тогда РасходнаяНакладная.Подразделение.ЦифровойИндексОбособленногоПодразделения
	|		Иначе """" Конец КАК ЦифровойИндексОбособленногоПодразделения
	|	,Выбор когда &ИспользоватьФаксимиле = ИСТИНА
	|		Тогда ЗНАЧЕНИЕ(Перечисление.ДаНет.Да)
	|		Иначе ЗНАЧЕНИЕ(Перечисление.ДаНет.Нет) Конец КАК ИспользоватьФаксимиле
	|	,Выбор когда РасходнаяНакладная.Организация.ГоловнаяОрганизация <> Значение(Справочник.Организации.ПустаяСсылка)
	|			Тогда РасходнаяНакладная.Организация
	|		когда РасходнаяНакладная.Подразделение.ГоловнаяОрганизация <> Значение(Справочник.Организации.ПустаяСсылка)
	|				И РасходнаяНакладная.Подразделение.ГоловнаяОрганизация = РасходнаяНакладная.Организация
	|			Тогда РасходнаяНакладная.Подразделение
	|		Иначе РасходнаяНакладная.Организация Конец КАК ОбособленноеПодразделениеПоставщика
	|	,Выбор когда РасходнаяНакладная.Организация.ЮридическоеФизическоеЛицо = Значение(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо) 
	|		Тогда Истина
	|		Иначе Ложь Конец КАК ЭтоФизическоеЛицо
	|	,Выбор Когда РасходнаяНакладная.НалогообложениеНДС = Значение(Перечисление.ТипыНалогообложенияНДС.НеОблагаетсяНДС)
	|		Тогда 2
	|		Иначе 1 Конец КАК СтатусУПД
	|	,Ложь КАК ЭтоСводныйСчетФактура
	|	,Ложь КАК ЭтоКорректировка
	|	,РасходнаяНакладная.БанковскийСчет КАК БанковскийСчет
	|	,Представление(РасходнаяНакладная.Подразделение) КАК ПредставлениеПодразделения
	|	,Представление(РасходнаяНакладная.СтруктурнаяЕдиница) КАК ПредставлениеСкладаСписания
	|	,Выбор когда РасходнаяНакладная.Грузоотправитель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			Тогда РасходнаяНакладная.Грузоотправитель
	|		Когда РасходнаяНакладная.Организация.ГоловнаяОрганизация <> Значение(Справочник.Организации.ПустаяСсылка)
	|			Тогда РасходнаяНакладная.Организация 	
	|		Когда РасходнаяНакладная.Подразделение.ГоловнаяОрганизация <> Значение(Справочник.Организации.ПустаяСсылка)
	|				И РасходнаяНакладная.Подразделение.ГоловнаяОрганизация = РасходнаяНакладная.Организация
	|			Тогда РасходнаяНакладная.Подразделение
	|		Иначе РасходнаяНакладная.Организация Конец КАК Грузоотправитель
	|	,Выбор когда РасходнаяНакладная.Контрагент.ГоловнойКонтрагент <> Значение(Справочник.Контрагенты.ПустаяСсылка)
	|			И РасходнаяНакладная.Контрагент.ГоловнойКонтрагент.СтранаРегистрации = Значение(Справочник.СтраныМира.Россия)
	|		Тогда РасходнаяНакладная.Контрагент.ГоловнойКонтрагент
	|		Иначе РасходнаяНакладная.Контрагент Конец КАК Контрагент
	|	,Выбор когда РасходнаяНакладная.Контрагент.ГоловнойКонтрагент <> Значение(Справочник.Контрагенты.ПустаяСсылка)
	|			И РасходнаяНакладная.Контрагент.ГоловнойКонтрагент.СтранаРегистрации = Значение(Справочник.СтраныМира.Россия)
	|		Тогда РасходнаяНакладная.Контрагент.ГоловнойКонтрагент
	|		Иначе РасходнаяНакладная.Контрагент Конец КАК Покупатель
	|	,Выбор когда РасходнаяНакладная.Грузополучатель.ГоловнойКонтрагент <> Значение(Справочник.Контрагенты.ПустаяСсылка)
	|			И РасходнаяНакладная.Грузополучатель.ГоловнойКонтрагент = РасходнаяНакладная.Контрагент
	|			И РасходнаяНакладная.Грузополучатель.ГоловнойКонтрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|		Тогда РасходнаяНакладная.Грузополучатель
	|		Иначе РасходнаяНакладная.Контрагент Конец КАК ОбособленноеПодразделениеПокупателя
	|	,РасходнаяНакладная.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента
	|	,Выбор когда РасходнаяНакладная.Грузополучатель <> Значение(Справочник.Контрагенты.ПустаяСсылка)
	|		Тогда РасходнаяНакладная.Грузополучатель
	|		Иначе РасходнаяНакладная.Контрагент Конец КАК Грузополучатель
	|	,РасходнаяНакладная.АдресДоставки КАК АдресДоставки
	|	,РасходнаяНакладная.КонтактноеЛицоПодписант.Наименование КАК РасшифровкаПодписиКонтрагента
	|	,РасходнаяНакладная.ДоверенностьНомер КАК ДоверенностьНомер
	|	,РасходнаяНакладная.ДоверенностьДата КАК ДоверенностьДата
	|	,РасходнаяНакладная.ДоверенностьВыдана КАК ДоверенностьВыдана
	|	,РасходнаяНакладная.ДоверенностьЛицо КАК ДоверенностьЛицо
	|	,РасходнаяНакладная.Договор.Представление КАК Основание
	|	,Выбор когда &ПечатнаяФормаТолькоВРублях = Истина 
	|		Тогда НациональнаяВалюта.Значение
	|		Иначе РасходнаяНакладная.ВалютаДокумента Конец КАК ВалютаДокумента
	|	,Выбор когда &ПечатнаяФормаТолькоВРублях = Истина 
	|		Тогда НациональнаяВалюта.Значение.НаименованиеПолное
	|		Иначе РасходнаяНакладная.ВалютаДокумента.НаименованиеПолное Конец КАК ВалютаНаименование
	|	,Выбор когда &ПечатнаяФормаТолькоВРублях = Истина 
	|		Тогда НациональнаяВалюта.Значение.Код
	|		Иначе РасходнаяНакладная.ВалютаДокумента.Код Конец КАК ВалютаКод
	|	,РасходнаяНакладная.СуммаВключаетНДС КАК СуммаВключаетНДС
	|	,РасходнаяНакладная.НДСВключатьВСтоимость КАК НДСВключатьВСтоимость
	|	,РасходнаяНакладная.Курс КАК Курс
	|	,РасходнаяНакладная.Кратность КАК Кратность
	|	,РасходнаяНакладная.ОснованиеПечати КАК ПредставлениеОснования
	|	,РасходнаяНакладная.ОснованиеПечатиСсылка КАК ОснованиеПечатиСсылка
	|	,Выбор когда РасходнаяНакладная.ОснованиеПечатиСсылка ССЫЛКА Справочник.ДоговорыКонтрагентов
	|		Тогда РасходнаяНакладная.ОснованиеПечатиСсылка.НомерДоговора
	|		Иначе РасходнаяНакладная.ОснованиеПечатиСсылка.Номер Конец КАК ОснованиеНомер
	|	,Выбор когда РасходнаяНакладная.ОснованиеПечатиСсылка ССЫЛКА Справочник.ДоговорыКонтрагентов
	|		Тогда РасходнаяНакладная.ОснованиеПечатиСсылка.ДатаДоговора
	|		Иначе РасходнаяНакладная.ОснованиеПечатиСсылка.Дата Конец КАК ОснованиеДата
	|	,Неопределено КАК ТранспортнаяНакладнаяНомер
	|	,Неопределено КАК ТранспортнаяНакладнаяДата
	|	,РасходнаяНакладная.ПодписьРуководителя.Должность КАК ДолжностьРуководителя
	|	,РасходнаяНакладная.ПодписьРуководителя.РасшифровкаПодписи КАК РасшифровкаПодписиРуководителя
	|	,РасходнаяНакладная.ПодписьРуководителя.Факсимиле КАК ФаксимилеРуководителя
	|	,РасходнаяНакладная.ПодписьГлавногоБухгалтера.РасшифровкаПодписи КАК РасшифровкаПодписиГлавногоБухгалтера
	|	,РасходнаяНакладная.ПодписьГлавногоБухгалтера.Факсимиле КАК ФаксимилеГлавногоБухгалтера
	|	,РасходнаяНакладная.ПодписьКладовщика.Должность КАК ДолжностьКладовщика
	|	,РасходнаяНакладная.ПодписьКладовщика.РасшифровкаПодписи КАК РасшифровкаПодписиКладовщика
	|	,РасходнаяНакладная.ПодписьКладовщика.Факсимиле КАК ФаксимилеКладовщика
	|	,РасходнаяНакладная.Вес КАК Вес
	|	,РасходнаяНакладная.Объем КАК Объем
	|	,РасходнаяНакладная.Запасы.(
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура КАК Номенклатура,
	|		Содержание КАК Содержание,
	|		ВЫБОР
	|			КОГДА (ВЫРАЗИТЬ(РасходнаяНакладная.Запасы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))) = """"
	|				ТОГДА РасходнаяНакладная.Запасы.Номенклатура.Наименование
	|			ИНАЧЕ ВЫРАЗИТЬ(РасходнаяНакладная.Запасы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))
	|		КОНЕЦ КАК ПредставлениеНоменклатуры,
	|		Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|		Номенклатура.Код КАК ЗапасКод,
	|		Номенклатура.Код КАК Код,
	|		Номенклатура.Артикул КАК Артикул,
	|		Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияПоОКЕИ_Наименование,
	|		Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияПоОКЕИ_Код,
	|		Характеристика КАК Характеристика,
	|		Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		НЕОПРЕДЕЛЕНО КАК ВидУпаковки,
	|		ВЫБОР
	|			КОГДА РасходнаяНакладная.Запасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|				ТОГДА РасходнаяНакладная.Запасы.ЕдиницаИзмерения.Коэффициент
	|			ИНАЧЕ 1
	|		КОНЕЦ КАК КоэффициентЕдиницыИзмерения,
	|		Количество КАК Количество,
	|		НЕОПРЕДЕЛЕНО КАК КоличествоВОдномМесте,
	|		НЕОПРЕДЕЛЕНО КАК КоличествоМест,
	|		СтавкаНДС КАК СтавкаНДС,
	|		Вес КАК МассаБрутто,
	|		ВЫРАЗИТЬ((РасходнаяНакладная.Запасы.СуммаАвтоматическойСкидки + РасходнаяНакладная.Запасы.СуммаСкидкиНаценки) * РасходнаяНакладная.Курс / РасходнаяНакладная.Кратность КАК ЧИСЛО(15, 2)) КАК СуммаСкидкиПоСтроке,
	|		ВЫРАЗИТЬ(РасходнаяНакладная.Запасы.Цена * РасходнаяНакладная.Курс / РасходнаяНакладная.Кратность КАК ЧИСЛО(15, 2)) КАК Цена,
	|		ВЫРАЗИТЬ(РасходнаяНакладная.Запасы.Сумма * РасходнаяНакладная.Курс / РасходнаяНакладная.Кратность КАК ЧИСЛО(15, 2)) КАК Сумма,
	|		ВЫРАЗИТЬ(РасходнаяНакладная.Запасы.СуммаНДС * РасходнаяНакладная.Курс / РасходнаяНакладная.Кратность КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
	|		ВЫРАЗИТЬ(РасходнаяНакладная.Запасы.Всего * РасходнаяНакладная.Курс / РасходнаяНакладная.Кратность КАК ЧИСЛО(15, 2)) КАК Всего
	|	) КАК ТаблицаЗапасы,
	|	РасходнаяНакладная.ДобавленныеНаборы.(
	|		НоменклатураНабора КАК НоменклатураНабора,
	|		ХарактеристикаНабора КАК ХарактеристикаНабора,
	|		Количество КАК Количество,
	|		ВЫБОР
	|			КОГДА (ВЫРАЗИТЬ(НоменклатураНабора.НаименованиеПолное КАК СТРОКА(1000))) = """"
	|				ТОГДА НоменклатураНабора.Наименование
	|			ИНАЧЕ ВЫРАЗИТЬ(НоменклатураНабора.НаименованиеПолное КАК СТРОКА(1000))
	|		КОНЕЦ КАК ЗапасНабора,
	|		НоменклатураНабора.ВариантПечатиНабора КАК ВариантПечатиНабора,
	|		НоменклатураНабора.ТипНоменклатуры КАК ТипНоменклатурыНабора,
	|		НоменклатураНабора.Артикул КАК АртикулНабора,
	|		НоменклатураНабора.Код КАК КодНабора,
	|		НоменклатураНабора.ЕдиницаИзмерения КАК ЕдиницаИзмеренияНабора,
	|		НоменклатураНабора.ЕдиницаИзмерения.Код КАК КодЕдиницыИзмеренияНабора,
	|		ВЫБОР
	|			КОГДА НоменклатураНабора.ВариантПечатиНабора = ЗНАЧЕНИЕ(Перечисление.ВариантыПечатиНаборов.ТолькоНабор)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ КАК ВыводитьИтоги
	|	) КАК ТаблицаДобавленныеНаборы,
	|	РасходнаяНакладная.НоменклатураДоставки КАК НоменклатураДоставки,
	|	ПРЕДСТАВЛЕНИЕ(РасходнаяНакладная.НоменклатураДоставки) КАК ПредставлениеНоменклатурыДоставки,
	|	РасходнаяНакладная.НоменклатураДоставки.Код КАК КодНоменклатурыДоставки,
	|	РасходнаяНакладная.НоменклатураДоставки.Артикул КАК АртикулНоменклатурыДоставки,
	|	РасходнаяНакладная.СтоимостьДоставки КАК СтоимостьДоставки,
	|	РасходнаяНакладная.СтавкаНДСДоставки КАК СтавкаНДСДоставки,
	|	РасходнаяНакладная.СуммаНДСДоставки КАК СуммаНДСДоставки
	|ИЗ
	|	Документ.РасходнаяНакладная КАК РасходнаяНакладная
	|	,Константа.НациональнаяВалюта КАК НациональнаяВалюта
	|ГДЕ
	|	РасходнаяНакладная.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	РасходнаяНакладная.Запасы.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходнаяНакладнаяЗапасы.Ссылка КАК Ссылка,
	|	МАКСИМУМ(РасходнаяНакладнаяЗапасы.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ МаксНомерСтрокиЗапасы
	|ИЗ
	|	Документ.РасходнаяНакладная.Запасы КАК РасходнаяНакладнаяЗапасы
	|ГДЕ
	|	РасходнаяНакладнаяЗапасы.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходнаяНакладнаяЗапасы.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходнаяНакладнаяЗапасы.Номенклатура КАК Номенклатура,
	|	ВЫРАЗИТЬ(РасходнаяНакладнаяЗапасы.Содержание КАК СТРОКА(1000)) КАК Содержание,
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(РасходнаяНакладнаяЗапасы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))) = """"
	|			ТОГДА РасходнаяНакладнаяЗапасы.Номенклатура.Наименование
	|		ИНАЧЕ ВЫРАЗИТЬ(РасходнаяНакладнаяЗапасы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))
	|	КОНЕЦ КАК ПредставлениеНоменклатуры,
	|	РасходнаяНакладнаяЗапасы.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	РасходнаяНакладнаяЗапасы.Номенклатура.Код КАК ЗапасКод,
	|	РасходнаяНакладнаяЗапасы.Номенклатура.Артикул КАК Артикул,
	|	РасходнаяНакладнаяЗапасы.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияПоОКЕИ_Наименование,
	|	РасходнаяНакладнаяЗапасы.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияПоОКЕИ_Код,
	|	РасходнаяНакладнаяЗапасы.Характеристика КАК Характеристика,
	|	РасходнаяНакладнаяЗапасы.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	РасходнаяНакладнаяЗапасы.ЕдиницаИзмерения КАК ВидУпаковки,
	|	РасходнаяНакладнаяЗапасы.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА РасходнаяНакладнаяЗапасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|			ТОГДА РасходнаяНакладнаяЗапасы.ЕдиницаИзмерения.Коэффициент
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК КоэффициентЕдиницыИзмерения,
	|	РасходнаяНакладнаяЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	РасходнаяНакладнаяЗапасы.Вес КАК Вес,
	|	РасходнаяНакладнаяЗапасы.СуммаАвтоматическойСкидки + РасходнаяНакладнаяЗапасы.СуммаСкидкиНаценки КАК СуммаСкидкиПоСтроке,
	|	РасходнаяНакладнаяЗапасы.Цена КАК Цена,
	|	РасходнаяНакладнаяЗапасы.НомерСтроки КАК НомерСтроки,
	|	РасходнаяНакладнаяЗапасы.Сумма КАК Сумма,
	|	РасходнаяНакладнаяЗапасы.СуммаНДС КАК СуммаНДС,
	|	РасходнаяНакладнаяЗапасы.Всего КАК Всего,
	|	РасходнаяНакладнаяЗапасы.Ссылка КАК Ссылка,
	|	РасходнаяНакладнаяЗапасы.НоменклатураНабора КАК НоменклатураНабора,
	|	РасходнаяНакладнаяЗапасы.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ЛОЖЬ КАК ЭтоНабор,
	|	ВЫБОР
	|		КОГДА РасходнаяНакладнаяЗапасы.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|				И РасходнаяНакладнаяЗапасы.НоменклатураНабора.ВариантПечатиНабора = ЗНАЧЕНИЕ(Перечисление.ВариантыПечатиНаборов.НаборИКомплектующие)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НеобходимоВыделитьКакСоставНабора
	|	,РасходнаяНакладнаяЗапасы.СтранаПроисхождения КАК СтранаСсылка
	|	,Представление(РасходнаяНакладнаяЗапасы.СтранаПроисхождения) КАК СтранаПредставление
	|	,РасходнаяНакладнаяЗапасы.СтранаПроисхождения.Код КАК СтранаКод
	|	,РасходнаяНакладнаяЗапасы.НомерГТД.РегистрационныйНомер КАК ПредставлениеГТД
	|ПОМЕСТИТЬ ВременнаяТаблица_РасходнаяНакладнаяЗапасы
	|ИЗ
	|	Документ.РасходнаяНакладная.Запасы КАК РасходнаяНакладнаяЗапасы
	|ГДЕ
	|	РасходнаяНакладнаяЗапасы.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасходнаяНакладнаяЗапасы.НоменклатураДоставки,
	|	ВЫРАЗИТЬ(РасходнаяНакладнаяЗапасы.НоменклатураДоставки.НаименованиеПолное КАК СТРОКА(1000)),
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(РасходнаяНакладнаяЗапасы.НоменклатураДоставки.НаименованиеПолное КАК СТРОКА(1000))) = """"
	|			ТОГДА РасходнаяНакладнаяЗапасы.НоменклатураДоставки.Наименование
	|		ИНАЧЕ ВЫРАЗИТЬ(РасходнаяНакладнаяЗапасы.НоменклатураДоставки.НаименованиеПолное КАК СТРОКА(1000))
	|	КОНЕЦ,
	|	РасходнаяНакладнаяЗапасы.НоменклатураДоставки.ТипНоменклатуры,
	|	РасходнаяНакладнаяЗапасы.НоменклатураДоставки.Код,
	|	РасходнаяНакладнаяЗапасы.НоменклатураДоставки.Артикул,
	|	&ШтукаНаименование,
	|	&ШтукаКод,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.шт),
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.шт),
	|	1,
	|	1,
	|	РасходнаяНакладнаяЗапасы.СтавкаНДСДоставки,
	|	0,
	|	0,
	|	РасходнаяНакладнаяЗапасы.СтоимостьДоставки,
	|	МаксНомерСтрокиЗапасы.НомерСтроки + 1,
	|	РасходнаяНакладнаяЗапасы.СтоимостьДоставки,
	|	РасходнаяНакладнаяЗапасы.СуммаНДСДоставки,
	|	ВЫБОР
	|		КОГДА РасходнаяНакладнаяЗапасы.Ссылка.СуммаВключаетНДС
	|			ТОГДА РасходнаяНакладнаяЗапасы.СтоимостьДоставки
	|		ИНАЧЕ РасходнаяНакладнаяЗапасы.СтоимостьДоставки + РасходнаяНакладнаяЗапасы.СуммаНДСДоставки
	|	КОНЕЦ,
	|	РасходнаяНакладнаяЗапасы.Ссылка,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
	|	ЛОЖЬ,
	|	ЛОЖЬ
	|	,Неопределено
	|	,Неопределено
	|	,Неопределено
	|	,Неопределено
	|ИЗ
	|	Документ.РасходнаяНакладная КАК РасходнаяНакладнаяЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ МаксНомерСтрокиЗапасы КАК МаксНомерСтрокиЗапасы
	|		ПО (МаксНомерСтрокиЗапасы.Ссылка = РасходнаяНакладнаяЗапасы.Ссылка)
	|ГДЕ
	|	РасходнаяНакладнаяЗапасы.Ссылка В(&МассивОбъектов)
	|	И РасходнаяНакладнаяЗапасы.НоменклатураДоставки <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходнаяНакладнаяЗапасы.Номенклатура КАК Номенклатура
	|	,РасходнаяНакладнаяЗапасы.Содержание КАК Содержание
	|	,ЕСТЬNULL(ВТ_ДанныеEDI.НаименованиеПартнера, РасходнаяНакладнаяЗапасы.ПредставлениеНоменклатуры) КАК ПредставлениеНоменклатуры
	|	,РасходнаяНакладнаяЗапасы.ТипНоменклатуры КАК ТипНоменклатуры
	|	,ЕСТЬNULL(ВТ_ДанныеEDI.КодТовараПартнера, РасходнаяНакладнаяЗапасы.ЗапасКод) КАК ЗапасКод
	|	,ЕСТЬNULL(ВТ_ДанныеEDI.КодТовараПартнера, РасходнаяНакладнаяЗапасы.ЗапасКод) КАК Код
	|	,РасходнаяНакладнаяЗапасы.Артикул КАК Артикул
	|	,РасходнаяНакладнаяЗапасы.ЕдиницаИзмеренияПоОКЕИ_Наименование КАК ЕдиницаИзмеренияПоОКЕИ_Наименование
	|	,РасходнаяНакладнаяЗапасы.ЕдиницаИзмеренияПоОКЕИ_Код КАК ЕдиницаИзмеренияПоОКЕИ_Код
	|	,РасходнаяНакладнаяЗапасы.Характеристика КАК Характеристика
	|	,РасходнаяНакладнаяЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|	,РасходнаяНакладнаяЗапасы.ЕдиницаИзмерения КАК ВидУпаковки
	|	,РасходнаяНакладнаяЗапасы.Количество КАК Количество
	|	,РасходнаяНакладнаяЗапасы.КоэффициентЕдиницыИзмерения КАК КоэффициентЕдиницыИзмерения
	|	,НЕОПРЕДЕЛЕНО КАК КоличествоВОдномМесте
	|	,НЕОПРЕДЕЛЕНО КАК КоличествоМест
	|	,РасходнаяНакладнаяЗапасы.СтавкаНДС КАК СтавкаНДС
	|	,РасходнаяНакладнаяЗапасы.Вес КАК МассаБрутто
	|	,РасходнаяНакладнаяЗапасы.НомерСтроки КАК НомерСтроки
	|	,Выбор Когда РасходнаяНакладнаяЗапасы.Ссылка.ВалютаДокумента = &НациональнаяВалюта
	|			Тогда РасходнаяНакладнаяЗапасы.СуммаСкидкиПоСтроке
	|			Иначе Выразить(РасходнаяНакладнаяЗапасы.СуммаСкидкиПоСтроке * РасходнаяНакладнаяЗапасы.Ссылка.Курс / РасходнаяНакладнаяЗапасы.Ссылка.Кратность КАК ЧИСЛО(15, 2)) Конец КАК СуммаСкидкиПоСтроке
	|	,Выбор когда &ПечатнаяФормаТолькоВРублях 
	|		Тогда
	|			Выбор Когда РасходнаяНакладнаяЗапасы.Ссылка.ВалютаДокумента = &НациональнаяВалюта
	|					Тогда РасходнаяНакладнаяЗапасы.Цена
	|				Когда РублевыеСуммыДокументовВВалюте.Всего ЕСТЬ NULL 
	|					Тогда Выразить(РасходнаяНакладнаяЗапасы.Цена * РасходнаяНакладнаяЗапасы.Ссылка.Курс / РасходнаяНакладнаяЗапасы.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|				Иначе
	|					Выбор Когда РасходнаяНакладнаяЗапасы.Количество = 0
	|						Тогда (РублевыеСуммыДокументовВВалюте.Всего - РублевыеСуммыДокументовВВалюте.НДС)
	|						Иначе Выразить((РублевыеСуммыДокументовВВалюте.Всего - РублевыеСуммыДокументовВВалюте.НДС) / РасходнаяНакладнаяЗапасы.Количество КАК ЧИСЛО(15, 2)) Конец
	|			Конец
	|		Иначе РасходнаяНакладнаяЗапасы.Цена Конец КАК Цена
	|	,Выбор когда &ПечатнаяФормаТолькоВРублях 
	|		Тогда
	|			Выбор Когда РасходнаяНакладнаяЗапасы.Ссылка.ВалютаДокумента = &НациональнаяВалюта
	|					Тогда РасходнаяНакладнаяЗапасы.Сумма
	|				Когда РублевыеСуммыДокументовВВалюте.Всего ЕСТЬ NULL 
	|					Тогда Выразить(РасходнаяНакладнаяЗапасы.Сумма * РасходнаяНакладнаяЗапасы.Ссылка.Курс / РасходнаяНакладнаяЗапасы.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|				Иначе
	|					Выбор Когда РасходнаяНакладнаяЗапасы.Ссылка.СуммаВключаетНДС
	|						Тогда Выразить(РублевыеСуммыДокументовВВалюте.Всего КАК ЧИСЛО(15, 2))
	|						Иначе Выразить(РублевыеСуммыДокументовВВалюте.Всего - РублевыеСуммыДокументовВВалюте.НДС КАК ЧИСЛО(15, 2)) Конец
	|			Конец
	|		Иначе РасходнаяНакладнаяЗапасы.Сумма Конец КАК Сумма
	|	,Выбор когда &ПечатнаяФормаТолькоВРублях 
	|		Тогда
	|			Выбор Когда РасходнаяНакладнаяЗапасы.Ссылка.ВалютаДокумента = &НациональнаяВалюта
	|					Тогда РасходнаяНакладнаяЗапасы.СуммаНДС
	|				Когда РублевыеСуммыДокументовВВалюте.Всего ЕСТЬ NULL 
	|					Тогда Выразить(РасходнаяНакладнаяЗапасы.СуммаНДС * РасходнаяНакладнаяЗапасы.Ссылка.Курс / РасходнаяНакладнаяЗапасы.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|				Иначе РублевыеСуммыДокументовВВалюте.НДС
	|			Конец
	|		Иначе РасходнаяНакладнаяЗапасы.СуммаНДС Конец КАК СуммаНДС
	|	,Выбор когда &ПечатнаяФормаТолькоВРублях 
	|		Тогда
	|			Выбор Когда РасходнаяНакладнаяЗапасы.Ссылка.ВалютаДокумента = &НациональнаяВалюта
	|					Тогда РасходнаяНакладнаяЗапасы.Всего
	|				Когда РублевыеСуммыДокументовВВалюте.Всего ЕСТЬ NULL 
	|					Тогда Выразить(РасходнаяНакладнаяЗапасы.Всего * РасходнаяНакладнаяЗапасы.Ссылка.Курс / РасходнаяНакладнаяЗапасы.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|				Иначе РублевыеСуммыДокументовВВалюте.Всего
	|			Конец
	|		Иначе РасходнаяНакладнаяЗапасы.Всего Конец КАК Всего
	|	,РасходнаяНакладнаяЗапасы.Ссылка КАК Ссылка
	|	,РасходнаяНакладнаяЗапасы.НоменклатураНабора КАК НоменклатураНабора
	|	,РасходнаяНакладнаяЗапасы.ХарактеристикаНабора КАК ХарактеристикаНабора
	|	,РасходнаяНакладнаяЗапасы.ЭтоНабор КАК ЭтоНабор
	|	,РасходнаяНакладнаяЗапасы.НеобходимоВыделитьКакСоставНабора КАК НеобходимоВыделитьКакСоставНабора
	|	,РасходнаяНакладнаяЗапасы.СтранаСсылка
	|	,РасходнаяНакладнаяЗапасы.СтранаПредставление
	|	,РасходнаяНакладнаяЗапасы.СтранаКод
	|	,РасходнаяНакладнаяЗапасы.ПредставлениеГТД
	|	,Выбор когда РасходнаяНакладнаяЗапасы.Номенклатура.ТипНоменклатуры = Значение(Перечисление.ТипыНоменклатуры.Запас)
	|		Тогда РасходнаяНакладнаяЗапасы.Номенклатура.ТоварнаяНоменклатураВЭД.Код
	|		Иначе Неопределено Конец КАК КодТНВЭД
	|ИЗ
	|	ВременнаяТаблица_РасходнаяНакладнаяЗапасы КАК РасходнаяНакладнаяЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РублевыеСуммыДокументовВВалюте КАК РублевыеСуммыДокументовВВалюте
	|		ПО РасходнаяНакладнаяЗапасы.Ссылка = РублевыеСуммыДокументовВВалюте.Регистратор
	|			И РасходнаяНакладнаяЗапасы.НомерСтроки = РублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДанныеEDI КАК ВТ_ДанныеEDI
	|		ПО РасходнаяНакладнаяЗапасы.Ссылка.Контрагент = ВТ_ДанныеEDI.Партнер
	|			И РасходнаяНакладнаяЗапасы.Номенклатура = ВТ_ДанныеEDI.Номенклатура
	|ГДЕ
	|	РасходнаяНакладнаяЗапасы.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	Результат = МассивРезультатов[1].Выгрузить();
	
	ТЗСтрок = МассивРезультатов[4].Выгрузить();
	Для Каждого ТекущаяСтрока Из Результат Цикл
		
		НайденныеСтроки = ТЗСтрок.НайтиСтроки(Новый Структура("Ссылка", ТекущаяСтрока.Ссылка));
		Если НайденныеСтроки.Количество() > 0 Тогда
			ТекущаяСтрока.ТаблицаЗапасы = ТЗСтрок.Скопировать(НайденныеСтроки);
		КонецЕсли;
		
	КонецЦикла;
	
	// Наборы
	НаборыСервер.КомпоноватьТабличнуюЧастьПоНаборам(Результат, "ТаблицаЗапасы");
	
	Возврат Результат;
	
КонецФункции

